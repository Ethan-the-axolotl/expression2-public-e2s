@name Define Library
# Author -- Vurv
# Source -- https://github.com/Vurv78/expression2-public-e2s
# Look at the Example code provided here https://github.com/Vurv78/expression2-public-e2s/blob/master/E2%20Libraries/Define/Example
# and save this file somewhere in your expression2 files.
# Updated -- v1.3 (Made the code 10x better, you only need to #include it, no need for an initializing function, also only runs once thanks to exit())

local Keyword = "define" # If the keyword is spaghetti, it will look like #spaghetti is 69 in code, making "is" be replaced by 69.

#ifndef entity:remoteSetCode(string)
    hint("You need remoteSetCode for this to work! Ask your server owner to enable the default remoteUploader extension!",5)
    selfDestruct()
#endif

if(getCodeIncludes():count()<1){error("This code must be #include'd")}

function string string:patternSafe(){ # Might not be necessary if you replace replaceRE with replace()
    local Kw = "%.*-+()^$?":explode("")
    local S = This
    foreach(K,V:string = Kw){
        S = S:replace(V,"%"+V)
    }
    return S
}
local Code = getCode()
local LocFN = getCodeIncludes():keys()[1,string] # Cool way to get local file name
local Defs = Code:gmatch("#"+Keyword+" (.-) (.-)\n")
Code = Code:replaceRE("#include%s*\""+LocFN+"\"","")
foreach(K:number,D:array = Defs){
    local Rep = D[1,string]:patternSafe()
    local RepTo = D[2,string]
    Code = Code:replaceRE("\""+Rep+"\"",RepTo)
}
entity():remoteSetCode(Code)
exit()


