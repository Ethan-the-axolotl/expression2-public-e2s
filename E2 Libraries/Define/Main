@name define on self
# Author -- Vurv
# Source -- https://github.com/Vurv78/expression2-public-e2s
# Look at the Example code provided here https://github.com/Vurv78/expression2-public-e2s/blob/master/E2%20Libraries/Define/Example
# and save this file somewhere in your expression2 files.
# Updated -- v1.2 ( Added an option (RemoveDefines) 1 to remove all of the #defines post-defining)
# Note that this chip doesn't work with stuff like type-based function calls.
# Also note that this chip may run twice the first time around because e2's remoteSetCode doesn't stop the current instance, which kinda sucks

#ifndef entity:remoteSetCode(string)
    hint("You need remoteSetCode for this to work! Ask your server owner to enable the default remoteUploader extension!",5)
    selfDestruct()
#endif
function string string:patternSafe(){
    local Kw = "%.*-+()^$?":explode("")
    local S = This
    foreach(K,V:string = Kw){
        S = S:replace(V,"%"+V)
    }
    return S
}
function definesInit([Code Keyword]:string){
    local LocFN = getCodeIncludes():keys()[1,string] # Cool way to get local file name
    print("Initializing defines!")
    local Defs = Code:gmatch("#"+Keyword+" (.-) (.-)\n")
    Code = Code:replaceRE("#include%s*\""+LocFN+"\"","")
    Code = Code:replaceRE("definesInit%(%s*getCode%(%s*%).-%)","")
    foreach(K:number,D:array = Defs){
        local Rep = D[1,string]:patternSafe()
        local RepTo = D[2,string]
        Code = Code:replaceRE("\""+Rep+"\"",RepTo)
    }
    entity():remoteSetCode(Code)
}
function definesInit([Code Keyword]:string,RemoveDefines){
    local LocFN = getCodeIncludes():keys()[1,string] # Cool way to get local file name
    print("Initializing defines!")
    local Defs = Code:gmatch("#"+Keyword+" (.-) (.-)\n")
    Code = Code:replaceRE("#include%s*\""+LocFN+"\"","")
    Code = Code:replaceRE("definesInit%(%s*getCode%(%s*%).-%)","")
    foreach(K:number,D:array = Defs){
        local Rep = D[1,string]:patternSafe()
        local RepTo = D[2,string]
        Code = Code:replaceRE("\""+Rep+"\"",RepTo)
    }
    if(RemoveDefines){Code=Code:replaceRE("#"+Keyword+" .- .-\n","")}
    entity():remoteSetCode(Code)
}


